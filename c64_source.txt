39168 "PRESS RETURN TO CONTINUE";
86 GET X$:IF X$=""THEN 86
87 PRINT X$:SYS TE:POKE 53249,0:POKE 53251,0:RETURN
88 REM-GET RESPONSE TO YES/NO
89 PRINT" (Y/N)?";
90 GET X$:IF X$=""THEN 90
91 PRINT X$:Y=-(X$="Y"):RETURN
110 REM-RANGE CHECK
112 IF X<0 THEN X=0
114 IF Y<0 THEN Y=0
116 RETURN
140 FOR X=0 TO 350:POKE CO,1:POKE XA,P%(P,0):POKE YA,P%(P,1):SYS P1
141 POKE CO,0:FOR Y=0 TO N1:IF P<>Y THEN POKE XA,P%(Y,0):POKE YA,P%(Y,1):SYS P1
142 NEXT:GET X$:IF X$<>""THEN X=2000
143 FOR Y=0 TO N1:POKE XA,P%(Y,0):POKE YA,P%(Y,1):SYS S2:SYS P1:NEXT
145 FOR Y=0 TO 5:NEXT:NEXT:RETURN
150 IF I%(P,5)<=0 THEN 160
154 Z=INT(I%(P,5)*.08+2):PRINT Z;" OF YOUR WARRIORS HAVE JUST DIED OF":PRINT"{rvrs on}STARVATION{rvrs off}"
156 I%(P,5)=I%(P,5)-Z:IF I%(P,5)<0 THEN I%(P,5)=0
160 IF I%(P,3)<=0 OR I%(P,10)<=0 OR RND(1)>=.3 THEN 85
164 I%(P,10)=I%(P,10)-1:PRINT"YOUR SCOUTS HAVE SLAIN AND EATEN ONE OF YOUR MULES"
170 GOTO 85
200 REM-SPELL OF SEEING
205 SYS AL:SYS GR
215 POKE CO,1:FOR I=0 TO N1:POKE XA,P%(I,0):POKE YA,P%(I,1):SYS P1:NEXT
220 RETURN
230 REM-OUTPUT A PLAYER'S VIEW
240 POKE PL,P:SYS SH:SYS GR
250 POKE CO,1:POKE XA,P%(P,0):POKE YA,P%(P,1):SYS P1:PRINT CS$:RETURN
260 REM-MAGIC MAP(I=1) OR CLOSE-UP(I=0)
270 X1=P%(P,0)-5:Y1=P%(P,1)-5
272 IF Y1<0 THEN Y1=0
273 IF X1<0 THEN X1=0
274 IF I THEN Z=PEEK(PL):POKE PL,255
275 PRINT"{clear}";
276 FOR Y=Y1 TO Y1+9:FOR X=X1 TO X1+9
277 POKE XA,X:POKE YA,Y:SYS SE
278 PRINT T$(PEEK(CO));:NEXT:PRINT:NEXT
280 PRINT:IF I THEN POKE PL,Z
284 SX=27+(P%(P,0)-X1)*32:SY=48+(P%(P,1)-Y1)*16:XH=INT(SX/256):XL=SX-XH*256
285 IF SX<20 OR SY<40 OR SX>344 OR SY>194 THEN POKE 53249,0:GOTO 287
286 POKE 53248,XL:POKE 53249,SY:POKE 53264,PEEK(53264)AND 254 OR XH
287 IF I=0 THEN 295
288 SX=27+(S%(DF,0)-X1)*32:SY=48+(S%(DF,1)-Y1)*16:XH=INT(SX/256):XL=SX-XH*256
289 IF SX<20 OR SY<40 OR SX>344 OR SY>194 THEN POKE 53251,0:GOTO 295
290 POKE 53250,XL:POKE 53251,SY:POKE 53264,PEEK(53264)AND 253 OR(XH*2)
295 PRINT"{blue}";:RETURN
300 REM-MAIN LOGIC, MENU OUT
305 T=T+1
306 IF T=1 THEN 400
310 PRINT"{clear}TURN = ";T;"{down:10}":PRINT"WOULD YOU LIKE TO SAVE THE GAME";
320 GOSUB 88:IF Y=0 THEN 400
325 PRINT:PRINT"SAVE GAME UNDER WHAT LETTER (A-G)?";
330 GET X$:IF X$=""THEN 330
332 PRINT X$;:IF ASC(X$)>71 OR ASC(X$)<65 THEN PRINT"{left} {left}";:GOTO 330
337 PRINT:PRINT
345 OPEN 1,8,15,"I":PRINT# 1,"S0:"+X$:PRINT# 1,"S0:MAP."+X$:CLOSE 1
350 OPEN 2,8,2,"0:"+X$+",S,W":PRINT# 2,N1:FOR P=0 TO N1:PRINT# 2,N$(P):FOR X=0 TO 22
355 PRINT# 2,I%(P,X):NEXT:PRINT# 2,P%(P,0):PRINT# 2,P%(P,1):NEXT:FOR S=0 TO 3:FOR X=0 TO 3
360 PRINT# 2,K%(S,X):NEXT:PRINT# 2,S%(S,0):PRINT# 2,S%(S,1):NEXT:PRINT# 2,T:CLOSE 2
365 OPEN 2,8,2,"0:MAP."+X$+",S,W":FOR X=50176 TO 51999:PRINT# 2,PEEK(X):NEXT:CLOSE 2
400 FOR P=0 TO N1
401 PRINT"{clear}";N$(P);"'S TURN":PRINT:GOSUB 85:MV=3:I%(P,0)=I%(P,0)-I%(P,5)/10
402 IF I%(P,0)<1 THEN I%(P,0)=0:MV=1:PRINT"{clear}YOU HAVE NO MORE FOOD ";N$(P):GOSUB 150
403 POKE PL,P:IF I%(P,13)>1 THEN MV=MV+1
404 IF RND(1)<.15 THEN GOSUB 11000
405 IF I%(P,16)=0 THEN 407
406 I%(P,16)=0:PRINT:GOSUB 1080:PRINT"YOU HAVE BEEN RAIDED!!":PRINT:GOSUB 1080:GOSUB 85:MV=MV+(MV>1)
407 IF I%(P,5)>0 AND(I%(P,0)>0 OR RND(1)>=.3)THEN 409
408 PRINT"DO YOU WISH TO PRAY FOR HELP";:GOSUB 88:IF Y THEN GOSUB 720:GOSUB 500:GOTO 490
409 IF I%(P,18)>0 THEN MV=MV+1
410 IF MV<0 THEN MV=0
412 GOSUB 8000:PRINT"{clear}{cyan}              >MAIN MENU<":PRINT
415 PRINT"TURN";T;" :PLAYER#";P+1;" -> "N$(P):PRINT"MOVEMENT FACTORS REMAINING ->";
416 PRINT"{rvrs on}"MV"{left} {rvrs off}":PRINT:PRINT"   OPTIONS:":PRINT
420 PRINT"1 = LOOK AT MAP":PRINT:PRINT"2 = LOOK AT CLOSE UP MAP":PRINT:PRINT"I = INVENTORY":PRINT
422 PRINT"M = MOVE":PRINT:PRINT"U = USE SPELL":PRINT:PRINT"E = END TURN":PRINT:PRINT"L = LOOK AT MAGIC MAP"
425 PRINT:PRINT"ENTER OPTION---> {left}";
430 GET A$:IF A$=""THEN 430
432 PRINT A$:IF A$="1"THEN GOSUB 230:GOSUB 85:GOTO 410
440 IF A$="2"THEN I=0:GOSUB 260:GOSUB 85:GOTO 410
450 IF A$="I"THEN GOSUB 500:GOTO 410
460 IF A$="U"THEN 600
465 IF A$="E"THEN 490
470 IF A$="M"AND MV>0 THEN GOSUB 800:GOTO 410
472 IF A$="L"THEN GOSUB 950:GOTO 410
475 PRINT"{up:2}";:GOTO 425
490 NEXT:GOTO 300
500 PRINT"{clear}"TAB(15)"INVENTORY":PRINT
510 PRINT:PRINT"AMOUNT"TAB(10)":     ITEM"
520 FOR X=0 TO 11:IF I%(P,X)=0 THEN 532
530 PRINT"  "I%(P,X);TAB(10)":   "I$(X);:IF I%(P,X)>1 THEN PRINT"S";
531 PRINT
532 NEXT
533 FOR X=17 TO 22:IF I%(P,X)<1 THEN 536
534 PRINT"  "I%(P,X);TAB(10)":   "I$(X);:IF I%(P,X)>1 THEN PRINT"S";
535 PRINT
536 NEXT:DF=0
540 FOR X=12 TO 15:IF I%(P,X)=0 THEN 550
542 IF I%(P,X)>1 THEN PRINT"   1";TAB(10)":   "I$(X)
544 IF I%(P,X)=1 OR I%(P,X)=11 THEN DF=DF+1
550 NEXT:IF DF>0 THEN PRINT"  ";DF;TAB(10);":   MAGIC MAP";:IF DF>1 THEN PRINT"S";
590 PRINT:GOTO 85
600 PRINT"{clear}":IF I%(P,1)OR I%(P,8)OR I%(P,19)THEN 606
605 PRINT:PRINT"NO SPELLS AVAILABLE":PRINT:GOSUB 85:GOTO 410
606 IF I%(P,19)THEN PRINT"CAST "I$(19);:GOSUB 88:IF Y THEN GOSUB 775:SYS TE:GOTO 410
610 IF I%(P,8)=0 THEN 620
612 PRINT"CAST "I$(8);:GOSUB 88:IF Y=0 THEN 620
616 GOSUB 200:PRINT CS$"PRESS ANY KEY TO CANCEL!":GOSUB 140:I%(P,8)=0:SYS TE:GOTO 410
620 IF I%(P,1)<1 THEN 410
625 PRINT"CAST "I$(1);:GOSUB 88:IF Y=0 THEN 410
627 I%(P,1)=I%(P,1)-1
630 GOSUB 230:PRINT"ENTER (I)TO GO UP..(K)TO GO RIGHT.."
631 PRINT"(J)TO GO LEFT..(M)TO GO DOWN..":PRINT"(B)TO GO BACK..(T)TO TELEPORT..";
632 X=P%(P,0):Y=P%(P,1):POKE XA,X:POKE YA,Y:SYS S1:C1=PEEK(CO):C=8:C2=8
633 X1=X:Y1=Y
635 GET X$
638 IF X$="I"THEN Y=Y+(Y>0):GOTO 670
639 IF X$="M"THEN Y=Y-(Y<39):GOTO 670
642 IF X$="K"THEN X=X-(X<39):GOTO 670
644 IF X$="J"THEN X=X+(X>0):GOTO 670
646 IF X$="B"THEN X=P%(P,0):Y=P%(P,1):GOTO 670
648 IF X$="T"THEN P%(P,0)=X:P%(P,1)=Y:GOTO 680
660 IF C=C2 THEN C=C1:GOTO 665
662 C=C2
665 POKE CO,C:POKE XA,X:POKE YA,Y:SYS P1:GOTO 635
670 POKE CO,C1:SYS P1:POKE XA,X:POKE YA,Y:SYS S1:C1=PEEK(CO)
675 POKE CO,C2:SYS P1:C=C2:GOTO 635
680 GOSUB 9900:SYS TE:IF DF<>14 OR I%(P,9)THEN 685
682 PRINT"{clear}YOU HAVE TELEPORTED ONTO A LAKE WITHOUT":PRINT"A RAFT! ALL YOUR WARRIORS ";
683 PRINT"AND MULES HAVE";:GOSUB 1080:PRINT"> DROWNED <":GOSUB 84:I%(P,5)=0:I%(P,10)=0
685 X=P%(P,0):Y=P%(P,1):GOSUB 832:GOTO 410
700 STOP:REM--JUST TO CHECK
720 IF RND(1)<.3 THEN PRINT"SORRY. NOT THIS TIME":GOTO 85
725 GOSUB 1100:GOSUB 760:GOTO 85
740 FOR I=0 TO N1:IF W%(I)>0 THEN X=I
742 NEXT:RETURN
750 Z=1:IF RND(1)<.5 THEN Z=-1
755 X1=INT(I%(X,5)*RND(1)*.3*Z):X2=I%(X,5)+X1:RETURN
760 IF I%(P,5)>9 THEN 765
763 I%(P,5)=I%(P,5)+15+RND(1)*10:PRINT"SOME "I$(5)"S HERE JOIN THE RANKS"
765 IF I%(P,0)>9 THEN 770
767 I%(P,0)=I%(P,0)+15+RND(1)*10:PRINT"THE GOOD MONKS GIVE YOU "I$(0)"S"
770 RETURN
775 PRINT"{clear}":FOR X=1 TO 4:PRINT X" = "I$(X+11):NEXT
777 PRINT"ENTER THE NUMBER OF THE ITEM YOU SEEK->";
780 GET X$:X=VAL(X$):IF X<1 OR X>4 THEN 780
785 GOSUB 230:PRINT"PRESS ANY KEY TO CONTINUE"
786 I%(P,19)=0
787 POKE CO,11:POKE XA,S%(X-1,0):POKE YA,S%(X-1,1):SYS P1
788 FOR S=0 TO 50:GET X$:IF X$<>""THEN S=2000
790 NEXT:IF S>1000 THEN RETURN
795 POKE CO,8:POKE XA,S%(X-1,0):POKE YA,S%(X-1,1):SYS P1
796 FOR S=0 TO 50:GET X$:IF X$<>""THEN S=2000
797 NEXT:IF S>1000 THEN RETURN
799 GOTO 787
800 I=0:GOSUB 260
805 PRINT"ENTER (1= N) (2= NE) (3= E) (4= SE)":PRINT"(5= S) (6= SW) (7= W) (8= NW) "
806 PRINT"(S)TO STAY WHERE YOU ARE..";
810 X=0:Y=0:GET X$:IF X$=""THEN 810
811 IF X$="1"THEN Y=Y-1:GOTO 822
812 IF X$="5"THEN Y=Y+1:GOTO 822
813 IF X$="7"THEN X=X-1:GOTO 822
814 IF X$="3"THEN X=X+1:GOTO 822
815 IF X$="S"THEN 822
816 IF VAL(X$)<1 OR VAL(X$)>8 OR MV<2 THEN 810
817 MV=MV-1:IF X$="2"THEN X=X+1:Y=Y-1:GOTO 822
818 IF X$="4"THEN X=X+1:Y=Y+1:GOTO 822
819 IF X$="6"THEN X=X-1:Y=Y+1:GOTO 822
820 IF X$="8"THEN X=X-1:Y=Y-1
822 X=X+P%(P,0):Y=Y+P%(P,1):GOSUB 110
823 GOSUB 9900
824 IF DF=14 AND I%(P,9)<1 THEN PRINT:PRINT"{rvrs on}NO RAFT! TRY AGAIN{rvrs off}";:GOTO 810
830 P%(P,0)=X:P%(P,1)=Y:POKE 53249,0
832 SYS TE:IF DF=14 THEN PRINT"{clear}"TAB(18)"LAKE":GOSUB 1000:GOTO 890
835 IF DF=4 THEN PRINT"{clear}"TAB(15)"SANCTUARY":GOSUB 1100:GOSUB 760:PRINT"'GO IN PEACE'":PRINT:PRINT:GOSUB 85:GOTO 890
840 IF DF=7 THEN PRINT"{clear}"TAB(17)"TOWN":PRINT:GOSUB 2000:GOTO 890
850 IF DF=9 THEN PRINT"{clear}"TAB(16)"MOUNTAINS":GOSUB 3000:GOTO 890
855 IF DF=5 THEN PRINT"{clear}"TAB(17)"FOREST":GOSUB 4000:GOTO 890
860 IF DF=6 THEN PRINT"{clear}INVADING THE FORTRESS OF THE WITCH KING":GOSUB 6000
890 MV=MV-1
900 REM
910 Z=I%(P,3)/2+1:X=P%(P,0)-Z:Y=P%(P,1)-Z:GOSUB 110:X1=X:Y1=Y:X2=X+2*Z:Y2=Y+2*Z
911 IF X2>39 THEN X2=39
912 IF Y2>39 THEN Y2=39
915 X1=INT(X1):Y1=INT(Y1):X2=INT(X2):Y2=INT(Y2):POKE PL,P
916 POKE XA,X1:POKE YA,Y1:POKE XB,X2:POKE YB,Y2:SYS SA:RETURN
950 Z=0:FOR X=12 TO 15:IF I%(P,X)=1 OR I%(P,X)=11 THEN Z=Z+1
955 NEXT:IF Z=0 THEN PRINT"{clear}YOU HAVE NO MAGIC MAPS":PRINT:GOTO 85
958 PRINT"{clear}YOU HAVE ";Z;" MAP";:IF Z>1 THEN PRINT"S";
959 PRINT:PRINT
960 FOR X=1 TO Z:PRINT X" = MAP#"X:NEXT:PRINT:PRINT"WHICH MAP DO YOU WISH TO LOOK AT?";
965 GET X$:Y=VAL(X$):IF Y<1 OR Y>Z THEN 965
967 PRINT" "X$:FOR X=12 TO 15:IF I%(P,X)=1 OR I%(P,X)=11 THEN Y=Y-1:IF Y=0 THEN Z=X-12:X=20
970 NEXT:X1=S%(Z,0)-5:Y1=S%(Z,1)-5
975 I=1:DF=Z:GOSUB 272:GOTO 85
1000 POKE SN,0:SYS TU:RETURN
1020 POKE SN,1:SYS TU:RETURN
1040 POKE SN,2:SYS TU:RETURN
1080 POKE LO,229:POKE HI,14:POKE DU,200:SYS TN
1082 POKE LO,27:POKE HI,13:POKE DU,200:SYS TN
1084 POKE LO,229:POKE HI,14:POKE DU,50:SYS TN:RETURN
1090 POKE LO,79:POKE HI,27:POKE DU,30:SYS TN
1092 POKE LO,247:POKE HI,40:POKE DU,100:SYS TN
1094 POKE LO,79:POKE HI,27:POKE DU,30:SYS TN
1096 POKE LO,247:POKE HI,40:POKE DU,150:SYS TN:RETURN
1100 FOR X=0 TO 15:POKE LO,139:POKE HI,65:POKE DU,9:SYS TN
1102 POKE LO,238:POKE HI,81:POKE DU,9:SYS TN:NEXT
1104 FOR X=0 TO 15:POKE LO,211:POKE HI,72:POKE DU,9:SYS TN
1106 POKE LO,162:POKE HI,93:POKE DU,9:SYS TN:NEXT:RETURN
1110 FOR N=0 TO 15:POKE LO,217:POKE HI,21:POKE DU,10:SYS TN:POKE LO,53:POKE HI,25:POKE DU,10:SYS TN:NEXT
1112 FOR N=0 TO 15:POKE LO,98:POKE HI,16:POKE DU,12:SYS TN:POKE LO,229:POKE HI,14:POKE DU,10:SYS TN:NEXT
1114 RETURN
1120 POKE SN,3:SYS TU:RETURN
1130 POKE SN,4:SYS TU:RETURN
1140 FOR N=0 TO 2:R=INT(RND(0)*1230)+10756:POKE LO,R-256*INT(R/256):POKE HI,INT(R/256)
1145 POKE DU,4:SYS TN:NEXT:RETURN
1200 FOR N=0 TO 3
1202 POKE 53280,PP:POKE 53281,PP
1210 FOR I=0 TO 3:POKE LO,218:POKE HI,12:POKE DU,1:SYS TN:NEXT
1212 POKE 53280,0:POKE 53281,0
1214 FOR I=0 TO N:POKE LO,218:POKE HI,12:POKE DU,1:SYS TN:NEXT:NEXT:RETURN
1300 FOR N=0 TO 20:POKE 53280,8:POKE 53281,8
1310 POKE LO,218:POKE HI,12:POKE DU,1:SYS TN
1320 POKE 53280,0:POKE 53281,0:NEXT
1390 RETURN
1400 PP=1:GOTO 1200
2000 GOSUB 1120:PRINT:PRINT"WELCOME TO THE MARKET PLACE :::::::"
2030 FOR X=0 TO 11:W%(X)=0:NEXT:W%(0)=RND(1)*2+1:IF RND(1)<.3 THEN W%(1)=RND(1)*100+50
2035 FOR X=2 TO 3:W%(X)=RND(1)*15+20:NEXT:IF I%(P,3)>9 THEN W%(3)=0
2040 W%(5)=2+RND(1)*5:W%(7)=RND(1)*15+10:W%(10)=RND(1)*15+10
2041 IF RND(1)<.65 THEN W%(9)=RND(1)*100+50
2042 IF RND(1)<.4 AND I%(P,11)<1 THEN W%(11)=RND(1)*100+50
2043 IF I%(P,2)>14 THEN W%(2)=0
2044 IF I%(P,7)>14 THEN W%(7)=0
2045 IF RND(1)<.1 AND I%(P,8)<1 THEN W%(8)=RND(1)*100+75
2047 PRINT TAB(8)"YOU HAVE {rvrs on}";I%(P,4);"{rvrs off} "I$(4);:IF I%(P,4)>1 THEN PRINT"S";
2048 PRINT:PRINT"THE FOLLOWING ITEMS ARE FOR SALE!!":PRINT:PRINT"ITEM NUMBER"TAB(15)"ITEM";
2049 PRINT TAB(28)"PRICE":PRINT
2050 FOR X=0 TO 11:IF W%(X)THEN PRINT TAB(2);X;TAB(10)":"TAB(14)I$(X)TAB(30)W%(X);:PRINT
2055 NEXT
2060 INPUT"ENTER ITEM NUMBER-->";X:IF X>11 THEN 2060
2062 IF W%(X)=0 THEN PRINT"ITEM NOT AVAILABLE":GOSUB 85:PRINT"{clear}":GOTO 2047
2067 PRINT"{clear}"TAB(8)"YOU HAVE {rvrs on}"I%(P,4)"{rvrs off} "I$(4);:IF I%(P,4)>1 THEN PRINT"S";
2070 PRINT:PRINT"MY PRICE FOR A "I$(X)" IS"W%(X):PRINT"IS THIS SUITABLE";:GOSUB 88:IF Y THEN 2090
2073 INPUT"HOW MUCH DO YOU OFFER";Y:Y=INT(Y)
2075 IF Y>=W%(X)THEN Y=W%(X):GOTO 2090
2077 X1=Y/W%(X):IF RND(1)>X1 THEN 2100
2080 IF RND(1)<X1 THEN W%(X)=Y:GOTO 2090
2082 PRINT"WILL YOU MEET ME HALF WAY";:DF=Y:GOSUB 88:IF Y THEN W%(X)=(W%(X)+DF)/2:GOTO 2090
2084 IF RND(1)<X1 THEN 2073
2086 GOTO 2100
2090 IF X=11 OR X=8 THEN Y=1:GOTO 2094
2093 PRINT:PRINT"AT $"W%(X)" EACH.":PRINT"HOW MANY "I$(X)"'S DO YOU WANT";:INPUT Y:IF Y<1 THEN Y=0
2094 X1=Y*W%(X):IF X1>I%(P,4)THEN PRINT"YOU DO NOT HAVE THE GOLD.":GOTO 2100
2095 I%(P,4)=I%(P,4)-X1:I%(P,X)=I%(P,X)+Y:PRINT"YOU NOW HAVE"I%(P,X)" "I$(X);:
2096 IF I%(P,X)>1 THEN PRINT"S";
2097 PRINT:IF X=9 THEN X=10:Y=Y*4:X1=0:GOTO 2095
2098 PRINT:GOTO 85
2100 GOSUB 1080:PRINT"THE MARKET IS CLOSED. COME BACK TOMORROW":GOTO 85
3000 MV=MV-1:Z=9:FOR X=0 TO 3:IF S%(X,0)=P%(P,0)AND S%(X,1)=P%(P,1)THEN Z=X
3010 NEXT:IF Z<4 THEN PRINT:PRINT TAB(17)O$(10):GOSUB 1110:S=10:GOSUB 1040:PRINT"{clear}":GOTO 5010
3100 IF RND(1)<.8 THEN 5000
3200 GOTO 85
4000 R=RND(1):IF R<.28 THEN 5000
4010 IF R<.3 THEN S=1:X7=I%(P,3)*3+5:GOTO 7000
4020 Z=I%(P,3)/2+1:X=P%(P,0)-Z:Y=P%(P,1)-Z:GOSUB 110:X1=X:Y1=Y:X2=X+2*Z:Y2=Y+2*Z
4035 I=0:FOR X=0 TO N1:W%(X)=0:NEXT:Y1=INT(Y1):Y2=INT(Y2):X1=INT(X1):X2=INT(X2)
4040 FOR S=0 TO N1:IF P=S THEN 4090
4045 IF P%(S,0)>=X1 AND P%(S,0)<=X2 AND P%(S,1)>=Y1 AND P%(S,1)<=Y2 THEN I=I+1:W%(S)=1
4090 NEXT
4095 FOR Z=0 TO N1:X=P%(Z,0):Y=P%(Z,1):GOSUB 9900:IF DF=4 THEN W%(Z)=0:I=I-1
4097 NEXT
4100 IF I<1 THEN RETURN
4110 PRINT"YOUR SCOUTS HAVE SPOTTED THE FOLLOWING":PRINT"ENEMY CAMP";:IF I>1 THEN PRINT"S";
4120 PRINT:PRINT:FOR X=0 TO N1:IF W%(X)>0 THEN PRINT"CAMP #"X" LEADER '"N$(X)"' STR=";:GOSUB 750:PRINT X2
4125 NEXT
4130 PRINT"WOULD YOU LIKE TO RAID ONE";:GOSUB 88:IF Y=0 THEN RETURN
4135 IF I<2 THEN GOSUB 740:GOTO 4150
4137 INPUT"WHICH ONE ?";X
4140 IF W%(X)<1 OR X>N1 THEN PRINT"SORRY, THAT CAMP'S NOT AROUND":PRINT:GOTO 4137
4150 PRINT"{clear}"TAB(17)"RAIDING":GOSUB 1020
4155 Y=0
4160 IF RND(1)<I%(P,7)*.1 THEN 4170
4164 PRINT"THE RAIDING PARTY RETURNS UNSUCCESSFUL:"
4166 PRINT:PRINT"'MY LIEGE, NO ONE WAS HURT.'":PRINT:GOSUB 1080:GOTO 85
4170 I%(X,16)=1:IF RND(1)<I%(X,3)*.08 THEN 4180
4174 Y=0:PRINT"A SUCCESSFUL RAID":GOSUB 1090:PRINT"'MY LIEGE, ALL RETURNED UNHURT":GOTO 4300
4180 Y=INT(I%(X,5)/10):IF Y>I%(P,7)THEN Y=I%(P,7)
4190 IF Y>0 THEN PRINT"MY LIEGE, WE HAVE LOST"Y" RAIDERS":GOSUB 1080
4200 I%(P,7)=I%(P,7)-Y
4210 IF I%(P,7)<1 THEN I%(P,7)=0:PRINT"THE RAID WAS UNSUCCESSFUL":GOSUB 1080:GOTO 85
4300 Y=I%(P,7)/40:PRINT"WE HAVE GARNERED THE FOLLOWING LOOT":PRINT:IF Y>.85 THEN Y=.85
4305 FOR I=0 TO 15:W%(I)=0:NEXT
4310 IF RND(1)<Y AND I%(X,1)THEN W%(1)=1:PRINT I$(1)
4320 IF RND(1)<Y AND I%(X,6)THEN W%(6)=1:PRINT I$(6)
4325 IF RND(1)<Y AND I%(X,8)THEN W%(8)=1:PRINT I$(8)
4330 IF RND(1)<Y AND I%(X,9)THEN W%(9)=1:PRINT I$(9)
4340 FOR S=12 TO 15:IF I%(X,S)=0 OR RND(1)>Y THEN 4349
4341 I=I%(X,S):IF I=1 THEN I=0:GOTO 4345
4342 IF I=11 THEN I=10:GOTO 4345
4343 PRINT I$(S):I=0:IF I%(P,S)>0 THEN I%(P,S)=11:GOTO 4348
4344 I%(P,S)=10:GOTO 4348
4345 PRINT"MAGIC MAP":IF I%(P,S)=0 THEN I%(P,S)=1:GOTO 4348
4346 IF I%(P,S)=10 THEN I%(P,S)=11
4348 I%(X,S)=I
4349 NEXT
4350 G=INT(Y*I%(X,4)):IF G<1 THEN PRINT"SORRY SIRE. NO LOOT HERE":GOTO 4370
4352 PRINT G" GOLD PIECE";:IF G>1 THEN PRINT"S";
4354 PRINT:I%(X,4)=I%(X,4)-G:I%(P,4)=I%(P,4)+G
4370 FOR S=0 TO 9:IF W%(S)=0 THEN 4380
4375 I%(X,S)=I%(X,S)-1:I%(P,S)=I%(P,S)+1:IF S=6 OR S=8 THEN I%(X,S)=0:I%(P,S)=1
4380 NEXT:GOSUB 1090:PRINT:GOTO 85
4500 STOP:REM--A CHECK
5000 PRINT"{clear}"TAB(15)"AN ENCOUNTER!!":GOSUB 1040
5005 S=INT(RND(1)*17):IF S=10 OR S=11 THEN 5005
5010 L=O(S):X=INT((I%(P,5)+I%(P,22))/L)
5011 X=INT(X+(X*.15*(RND(1)*3-1))):IF X<1 THEN X=1
5012 X7=X*L:IF S>14 AND X<2 THEN X=2
5020 PRINT"YOU HAVE COME UPON"X" "O$(S);:IF X>1 THEN PRINT"S";
5022 PRINT:D=0:D1=0:X3=1
5024 IF RND(1)<.2 THEN PRINT:PRINT"YOU HAVE BEEN SURPRISED!":GOSUB 1080:GOTO 5100
5025 PRINT:IF S=0 OR S>14 THEN PRINT"DO YOU WISH TO PARLEY";:GOSUB 88:IF Y=0 THEN 5070
5027 IF S>0 AND S<15 THEN 5070
5030 IF RND(1)<(I%(P,0)+I%(P,4))/100 THEN PRINT"THEY WILL JOIN THE RANKS!!":GOSUB 1090:GOTO 5300
5040 PRINT"THEY DO NOT WISH TO JOIN. BUT THEY WILL":PRINT"LEAVE YOU IN PEACE.":GOTO 85
5070 PRINT"WILL YOU RUN AWAY";:GOSUB 88
5075 IF Y AND RND(1)<.8 THEN GOSUB 1080:PRINT"     COWARD":CW=1:GOTO 1080
5080 IF Y THEN PRINT"THEY HAVE YOU NOW":GOSUB 1040
5100 FOR DF=0 TO 500:NEXT:PRINT"{clear}"TAB(10)"PRESS 'R' TO RETREAT!!":PRINT
5110 Y=X*L:Y1=I%(P,5)+I%(P,11)*20+RND(1)*15+I%(P,17)*15*X3+I%(P,22)*2
5115 IF S=10 AND I%(P,6)THEN Y1=Y1+50
5120 PRINT"THE BATTLE RAGES ON":PRINT:IF RND(1)>Y1*.012 THEN 5130
5125 GOSUB 1090:PRINT N$(P)"'S SIDE PRESSES THE ATTACK":D=D+RND(1)*Y1+1:GOSUB 9000
5130 IF RND(1)>Y*.01 THEN 5200
5134 PRINT:PRINT"THE "O$(S);:IF X>1 THEN PRINT"S ADVANCE";:GOTO 5140
5136 PRINT" ADVANCES";
5140 PRINT:D1=D1+RND(1)*Y*.4:GOSUB 1080:GOSUB 9500
5155 Z=INT(RND(1)*12):IF Z<2 OR Z=4 OR Z=5 OR Z=6 OR Z=8 OR Z=9 THEN 5182
5160 IF I%(P,Z)=0 THEN 5182
5170 I%(P,Z)=I%(P,Z)-1:PRINT"SIRE WE HAVE JUST LOST A "I$(Z):GOSUB 1080
5180 IF(Z=2 OR Z=3)AND RND(1)<.32 THEN 5160
5182 IF I%(P,21)=0 OR RND(1)>.4 THEN 5187
5183 I%(P,21)=I%(P,21)-1:PRINT"WE LOST AN ELF SIRE"
5184 IF I%(P,21)=1 THEN I%(P,21)=0:PRINT"SIRE WE LOST ANOTHER ELF"
5187 IF I%(P,22)=0 OR RND(1)>.4 THEN 5195
5190 I%(P,22)=I%(P,22)-1:PRINT"WE LOST A DWARF SIRE"
5193 IF I%(P,22)=1 THEN I%(P,22)=0:PRINT"SIRE WE LOST ANOTHER DWARF"
5195 R=RND(1):IF R<.2 THEN 5182
5197 IF R<.3 THEN 5155
5200 PRINT:PRINT"ROUND UPDATE:":PRINT:I%(P,5)=I%(P,5)-D1:IF I%(P,5)<1 THEN I%(P,5)=0
5202 IF I%(P,2)=0 OR D1<=0 OR RND(1)>(.35+I%(P,5)*.01)THEN 5208
5203 Z=INT(D1*I%(P,2)*.03+1):IF D1<Z THEN Z=D1
5204 Z=INT(Z):I%(P,5)=I%(P,5)+Z
5205 PRINT"YOUR "I$(2)"S RESTORE"Z" "I$(5);:IF Z>1 THEN PRINT"S";
5207 PRINT
5208 D1=0
5210 PRINT"YOU HAVE"I%(P,5)" "I$(5);:IF I%(P,5)<>1 THEN PRINT"S";
5215 PRINT:Z=INT(D/L):IF Z<1 THEN 5260
5220 X=X-Z:D=D-Z*L:IF D<0 THEN D=0
5260 IF X<1 THEN X=0
5262 PRINT"THEY HAVE"X" "O$(S);:IF X<>1 THEN PRINT"S";
5263 PRINT:PRINT
5265 IF X<1 THEN PRINT:PRINT"VICTORY IS YOURS!!":IF I%(P,5)<1 THEN PRINT"JUST BARELY!!"
5266 IF X<1 THEN GOSUB 1040:GOTO 7000
5267 IF I%(P,5)=0 THEN PRINT"YOU HAVE BEEN DEFEATED!!":GOSUB 1080:PRINT:GOTO 85
5271 IF X$="R"THEN 5070
5272 GET X$:IF X$="R"THEN 5070:REM--X$MAY BE STEPPED ON
5273 GOSUB 85:PRINT"{clear}PRESS <R> TO RETREAT.":PRINT
5275 FOR DF=0 TO 500:NEXT
5295 PRINT"{clear}":GOTO 5100
5300 IF S=0 THEN S=5:GOTO 5320
5310 S=S+6
5320 I%(P,S)=I%(P,S)+X
5390 GOTO 85
6000 GOSUB 1040:Y3=1
6010 X=I%(P,3):IF I%(P,12)>1 THEN X=X+75
6020 IF RND(I)>X*.01 THEN PRINT"YOU CANNOT FIND THE ENTRANCE":GOSUB 1080:GOTO 85
6030 PRINT:PRINT"A VOICE BOOMS FROM WITHIN THE FORTRESS"
6040 PRINT:PRINT"'PREPARE TO MEET YOUR DOOM'!!":GOSUB 1130
6045 Y2=2:X2=1:X3=1
6050 IF I%(P,13)>1 THEN Y2=1:Y3=Y3+1
6060 IF I%(P,14)>1 THEN X2=.6:Y3=Y3+1
6070 IF I%(P,15)>1 THEN X3=1.85:Y3=Y3+1
6100 PRINT:PX=2:PY=0
6102 PRINT"{clear}":GOSUB 6200:IF K%(PX,PY)=14 THEN 6102
6103 IF K%(PX,PY)<>15 THEN 6107
6104 PRINT"OOPS!! A TELEPORTER.":GOSUB 1080
6105 GOSUB 9899:IF DF=6 AND I%(P,9)<1 THEN 6105
6106 P%(P,0)=X:P%(P,1)=Y:RETURN
6107 IF K%(PX,PY)<>11 THEN 6111
6108 PRINT"{clear}YOU HAVE FOUND THE "O$(11)"!!":GOSUB 1040:PRINT
6109 PRINT"ALL YOUR MEN HAVE DISAPPEARED.":PRINT:PRINT"IT'S JUST YOU AND THE "O$(11)"!":PRINT
6110 GOSUB 85:GOTO 6700
6111 S=INT(RND(1)*10)+5:IF S=11 OR S=10 THEN 6111
6112 L=O(S):X=INT(((I%(P,5)+I%(P,22))/L+1)*Y2):L=L*X2:PRINT"{clear}":IF X<1 THEN X=1
6113 D=0:D1=0:PRINT X" "O$(S);:IF X>1 THEN PRINT"S CHARGE DOWN ON YOU":GOTO 6130
6120 PRINT" COMES RUSHING AT YOU"
6130 GOSUB 1040:CW=0:GOSUB 5100:IF I%(P,5)AND CW=0 THEN 6140
6135 PRINT"THE ATTACK HAS FAILED! HA-HA-HA":GOTO 85
6140 IF S<>11 THEN 6102
6145 PRINT"{clear}{down:12}YOU ARE VICTORIOUS!!":PRINT:PRINT"THE WITCH KING IS DEAD":GOSUB 1090
6147 PRINT"LONG LIVE KING "N$(P):PRINT:GOSUB 85
6150 GOSUB 1040:PRINT"{clear}{down:12}{rvrs on}THE WINNER IS "N$(P)" YOUR SCORE =";
6155 X=INT((100-T)/Y3):IF X<0 THEN X=0
6160 PRINT X:PRINT"{rvrs off}{down:3}":PRINT"PLAY AGAIN";:GOSUB 88:IF Y THEN RUN
6170 STOP
6200 E$="EAST":N$="NORTH":S$="SOUTH":W$="WEST":D$="DOORS ARE "
6205 ON PY+1 GOTO 6210,6220,6220,6230
6210 ON PX+1 GOTO 6400,6415,6415,6420
6220 ON PX+1 GOTO 6405,6440,6440,6425
6230 ON PX+1 GOTO 6410,6435,6435,6430
6400 GOSUB 6580:PRINT D$S$" AND "E$".":GOSUB 6500:IF X<0 OR Y<0 THEN GOSUB 6600:GOTO 6400
6402 GOTO 6495
6405 GOSUB 6580:PRINT D$N$", "S$" AND "E$".":GOSUB 6500:IF X<0 THEN GOSUB 6600:GOTO 6405
6407 GOTO 6495
6410 GOSUB 6580:PRINT D$N$" AND "E$".":GOSUB 6500:IF X<0 OR Y>0 THEN GOSUB 6600:GOTO 6410
6412 GOTO 6495
6415 GOSUB 6580:PRINT D$S$", "E$" AND "W$".":GOSUB 6500:IF Y<0 THEN GOSUB 6600:GOTO 6415
6417 GOTO 6495
6420 GOSUB 6580:PRINT D$S$" AND "W$".":GOSUB 6500:IF X>0 OR Y<0 THEN GOSUB 6600:GOTO 6420
6422 GOTO 6495
6425 GOSUB 6580:PRINT D$N$", "S$" AND "W$".":GOSUB 6500:IF X>0 THEN GOSUB 6600:GOTO 6425
6427 GOTO 6495
6430 GOSUB 6580:PRINT D$N$" AND "W$".":GOSUB 6500:IF Y>0 OR X>0 THEN GOSUB 6600:GOTO 6430
6432 GOTO 6495
6435 GOSUB 6580:PRINT D$N$", "E$" AND "W$".":GOSUB 6500:IF Y>0 THEN GOSUB 6600:GOTO 6435
6437 GOTO 6495
6440 GOSUB 6580:PRINT"EXITS ARE "N$", "S$", "E$" AND "W$".":GOSUB 6500
6495 PX=PX+X:PY=PY+Y:RETURN
6500 PRINT:PRINT"ENTER THE FIRST LETTER OF THE DIRECTION":PRINT"IN WHICH YOU WISH TO GO.-->";
6503 GET A$:IF A$=""THEN 6503
6505 X=0:Y=0:IF A$="W"THEN X=-1
6510 IF A$="S"THEN Y=1
6520 IF A$="E"THEN X=1
6540 IF A$="N"THEN Y=-1
6550 IF X OR Y THEN PRINT:RETURN
6560 PRINT:PRINT"YOU MUST PRESS N, S, E, OR W":PRINT:GOTO 6500
6580 PRINT"YOU ARE IN "B$(K%(PX,PY)):RETURN
6600 PRINT"YOU CAN'T GO IN THAT DIRECTION":FOR X=0 TO 1000:NEXT:RETURN
6700 FOR X=0 TO 1:FOR I=0 TO 4:A%(X,I)=0:NEXT:NEXT:X1=30:X2=40:Y1=12:Y2=8:IF I%(P,14)THEN X1=55
6725 GOSUB 6900
6730 I=.6:IF I%(P,13)THEN I=.4
6735 IF RND(1)<I THEN GOSUB 6785:GOTO 6740
6737 GOSUB 6780
6740 GOSUB 6800
6742 IF A<3 THEN 6750
6744 IF I=0 THEN X1=X1-Z/2:IF X1<5 THEN X1=5
6746 IF I=1 THEN X2=X2-Z/2:IF X2<5 THEN X2=5
6750 IF A%(0,0)>49 THEN GOSUB 1110:GOSUB 1080:GOTO 6950
6755 IF A%(1,0)<50 THEN 6730
6757 PRINT"{down}{rvrs on}"B$" IS DOWN!!{rvrs off}":PRINT"HIS BODY MELTS AWAY.":PRINT:S=11:X=0:GOSUB 7000:GOTO 6145
6780 X=X2:Y=Y1:A$=N$(P):B$="THE "+O$(11):I=1:RETURN
6785 X=X1:Y=Y2:B$=N$(P):A$="THE "+O$(11):I=0:RETURN
6800 R=RND(1):A=0:IF R>.8 THEN A=1
6802 IF R>.85 THEN A=2
6804 IF R>.9 THEN A=3
6806 IF R>.95 THEN A=4
6808 IF A%(I,1)>19 OR A%(I,2)>19 THEN X=X-5
6810 IF INT(RND(1)*100)<=X THEN GOSUB 1140:GOTO 1140
6815 IF A%(1-I,2)<20 OR A%(1-I,1)<20 THEN 6820
6817 PRINT A$" TRIES TO LIFT HIS SWORD":PRINT"IN HIS NOW USELESS HAND, BUT FAILS":GOTO 6895
6820 D$="THRUSTS":IF RND(1)<.5 THEN D$="CHOPS":GOSUB 1140
6825 PRINT A$" "D$" AT "B$:GOSUB 1140:PRINT"WITH HIS SWORD":IF D$="CHOPS"THEN 6840
6830 IF RND(1)<.7 THEN 6850
6835 IF A%(I,1)>19 THEN PRINT B$" MAKES A FEEBLE ATTEMPT":PRINT"TO PARRY THE BLOW":GOTO 6850
6837 PRINT B$" PARRIES THE BLOW!!":GOTO 6895
6840 IF RND(1)<.6 THEN 6850
6845 IF A%(I,1)<20 THEN 6847
6846 PRINT B$" MAKES A FEEBLE ATTEMPT":PRINT"TO BLOCK THE BLOW WITH HIS SHIELD.":GOTO 6850
6847 PRINT"BUT IT BOUNCES HARMLESSLY OFF":PRINT B$"'S SHIELD":GOTO 6895
6850 Z=INT(1+RND(1)*Y):IF I=1 OR I%(P,15)>1 THEN Z=Z+INT(1+RND(1)*Y)
6852 Z=Z-A%(1-I,2)/2:IF Z<1 THEN PRINT"THE BLOW GLANCES OFF "B$"'S":PRINT"ARMOR":GOTO 6895
6854 D$="NICKS":IF Z>5 THEN D$="CUTS INTO"
6856 IF Z>10 THEN D$="CHOPS INTO"
6860 PRINT:PRINT A$"'S SWORD "D$:PRINT B$"'S "A$(A):A%(I,A)=A%(I,A)+Z:GOSUB 1090
6890 IF A<>2 OR A%(I,A)<20 OR A%(I,A)-Z>19 THEN 6895
6893 PRINT B$" DROPS HIS SHIELD":PRINT"AND PLACES HIS SWORD IN HIS OTHER HAND"
6895 PRINT"{down:2}":GOSUB 85
6900 PRINT"{clear}"TAB(15)"---DAMAGE---":PRINT TAB(13)N$(P)TAB(29)O$(11)
6902 FOR X=0 TO 4:PRINT A$(X)TAB(12)"...";:I=A%(0,X):GOSUB 6910:PRINT I"%";:I=A%(1,X):GOSUB 6910
6904 PRINT TAB(30)I"%":NEXT:PRINT:RETURN
6910 Y=50:IF X=1 OR X=2 THEN Y=20
6911 IF X>2 THEN Y=30
6912 I=INT(I*100/Y):IF I>100 THEN I=100
6914 RETURN
6950 PRINT:PRINT"{down:2}THE LAST BLOW RENDERS YOU UNCONSCIOUS!!"PRINT:PRINT"YOU AWAKEN OUTSIDE THE ";
6955 PRINT"CASTLE.":PRINT:GOSUB 85
6960 FOR X=0 TO 22:IF X=3 OR X=5 OR X=11 THEN NEXT
6965 I%(P,X)=0:NEXT:MV=0:RETURN
7000 PRINT"YOU HAVE FOUND THE FOLLOWING ITEMS":PRINT
7005 IF S=10 THEN I%(P,6)=0
7010 IF RND(1)<.25 THEN I%(P,1)=I%(P,1)+1:PRINT I$(1)
7020 IF RND(1)<.2 AND I%(P,6)<1 THEN I%(P,6)=1:PRINT I$(6)
7025 IF RND(1)<.15 AND I%(P,8)<1 THEN I%(P,8)=1:PRINT I$(8)
7030 Z=INT(RND(1)*X7+X7)+10:IF S=10 OR S=12 THEN Z=Z*2
7031 IF S=11 THEN Z=2000
7033 PRINT Z" "I$(4);:IF Z>1 THEN PRINT"S";
7034 IF I%(P,4)+Z>30000 THEN I%(P,4)=30000:Z=0
7035 PRINT:I%(P,4)=I%(P,4)+Z
7040 IF RND(1)<.7 THEN 7090
7045 Z=INT(RND(1)*4)+12:IF I%(P,Z)=1 OR I%(P,Z)=11 THEN 7090
7050 PRINT"MAGIC MAP"
7060 IF I%(P,Z)>0 THEN I%(P,Z)=11:GOTO 7090
7070 I%(P,Z)=1
7090 GOSUB 7200:GOSUB 7300:IF S<>10 THEN 85
7100 Z=9:FOR X=0 TO 3:IF S%(X,0)=P%(P,0)AND S%(X,1)=P%(P,1)THEN Z=X
7110 NEXT:IF Z>3 THEN 85
7120 Z=Z+12:IF I%(P,Z)>1 THEN 85
7130 PRINT I$(Z):IF I%(P,Z)>0 THEN I%(P,Z)=11:GOTO 85
7140 I%(P,Z)=10:GOTO 85
7200 IF L<10 OR RND(1)>.3 THEN RETURN
7210 Z=INT(RND(1)*3)+17:IF I%(P,Z)>0 THEN RETURN
7215 IF Z=18 THEN MV=MV+1:IF MV<1 THEN MV=1
7220 I%(P,Z)=1:PRINT I$(Z):RETURN
7300 IF S<>11 THEN RETURN
7303 PRINT"THE CROWN OF THE WITCH KING"
7304 PRINT"THE SCEPTER OF THE WITCH KING":PRINT"THE ORB OF THE WITCH KING"
7306 PRINT"HUNDREDS OF GEMS AND PIECES OF JEWELRY"
7310 PRINT:RETURN
8000 PRINT"{clear}":Z=I%(P,3)*5+I%(P,5)*10+I%(P,10)*50+5:Y=0
8010 DF=I%(P,9)*4:IF DF=0 THEN 8050
8015 IF I%(P,10)<DF THEN PRINT"YOU JUST DROPPED A RAFT!":Y=1:I%(P,9)=I%(P,9)-1:GOTO 8010
8020 Z=Z-DF*50
8050 IF I%(P,4)>Z THEN PRINT"YOU HAD TO DROP"I%(P,4)-Z" "I$(4);:IF I%(P,4)-Z>1 THEN PRINT"S";
8060 PRINT:IF I%(P,4)>Z THEN I%(P,4)=Z:Y=1
8070 IF Y THEN 85
8080 RETURN
9000 IF I%(P,5)<1 OR RND(1)<.75 THEN GOSUB 9050:GOTO 9100
9005 GOSUB 9050:M=5:N=.8:GOSUB 9010:GOTO 9100
9010 PRINT:PRINT"YOUR "I$(M);:IF I%(P,M)>1 THEN PRINT"S ARE SMASHING THE ENEMY";:GOTO 9030
9020 PRINT"IS CRUSHING YOUR FOE";
9030 PRINT:GOSUB 1090:D=D+RND(1)*I%(P,M)*N:RETURN
9050 IF I%(P,22)<1 OR RND(1)<.5 THEN RETURN
9060 M=22:N=2:GOTO 9010
9100 IF I%(P,11)<1 OR RND(1)<.5 THEN 9300
9110 PRINT:PRINT"YOUR "I$(11)" HAS CAST A ";:DF=INT(RND(1)*4+1)
9115 ON DF GOSUB 9120,9130,9140,9150:GOSUB 1090:D=D+DF*RND(1)*1.3+1:GOTO 9300
9120 PRINT"LIGHTNING BOLT":DF=10:GOTO 1400
9130 PRINT"FIRE BALL":DF=15:PP=2:GOTO 1200
9140 PRINT"TOWER OF FLAME":DF=10:PP=8:GOTO 1200
9150 PRINT"DEATH RAY":DF=7:PP=14:GOTO 1200
9200 FOR DF=1 TO 500:NEXT:RETURN
9300 IF I%(P,21)<1 OR RND(1)<.65 THEN 9200
9310 PRINT:PRINT"YOUR "I$(21)"S HAVE CAST A ";:DF=INT(RND(1)*4+1)
9320 ON DF GOSUB 9120,9130,9140,9150:GOSUB 1090:D=D+DF*RND(1)+I%(P,21)/2:GOTO 9200
9500 IF S=8 OR S=9 OR S=11 OR RND(1)<.8 THEN 9600
9520 PRINT:PRINT"THE "O$(S);:IF X>1 THEN PRINT"S ARE CHARGING";:GOTO 9540
9530 PRINT" IS CHARGING";
9540 PRINT:GOSUB 1080:D1=D1+RND(1)*Y*.3
9600 IF S=6 OR S=8 OR S=9 OR S=10 OR S=11 THEN 9604
9601 IF S=12 THEN 9620
9602 IF S=15 THEN 10000
9603 GOTO 9200
9604 IF RND(1)<.6 THEN 9200
9610 PRINT:PRINT"THE "O$(S)" CASTS A ";:DF=INT(RND(1)*4+1)
9615 ON DF GOSUB 9120,9130,9140,9150:D1=D1+RND(1)*DF*1.2:GOSUB 1080:IF S<>10 THEN 9200
9620 IF RND(1)<.65 THEN 9200
9630 PRINT:PRINT"THE "O$(S)" BREATHES FIRE !!!":GOSUB 1110:D1=D1+RND(1)*O(S)*1.2:GOSUB 1300
9640 GOTO 9200
9899 X=P%(P,0)-INT(RND(1)*5):Y=P%(P,1)-INT(RND(1)*5)
9900 POKE XA,X:POKE YA,Y:SYS S2:DF=PEEK(CO):RETURN
10000 IF RND(1)<.7 THEN 9200
10010 PRINT:PRINT"THE "O$(S)"S CAST A FAERIE FIRE":D1=D1+RND(1)*10:PRINT:PP=12:GOSUB 1200:GOTO 9200
11000 M=21:N=18:IF RND(1)<.5 THEN M=22:N=17
11010 IF I%(P,N)<1 THEN RETURN
11020 I%(P,N)=0:I%(P,M)=0:PRINT:PRINT"SOME "I$(M)"S HAVE RUN OFF WITH"
11030 PRINT"YOUR "I$(N):PRINT:GOTO 85
20000 SYS 52257
20100 GOSUB 25000
20140 PRINT:PRINT"RECALL OLD GAME  (Y/N)?";
20150 GET X$:IF X$=""THEN 20150
20160 PRINT X$:Y=-(X$="Y"):NG=Y:IF Y THEN N1=3:GOTO 20180
20170 PRINT:INPUT"NUMBER OF PLAYERS (1 TO 4)             {left:13}";N%:N1=N%-1
20175 IF N%<1 OR N%>4 THEN PRINT"{up:2}";:GOTO 20170
20180 DIM I%(N1,22),P%(N1,1),S%(3,1),W%(22),O$(16),O(16),N$(N1),A%(1,4),A$(4)
20190 DIM K%(3,3),I$(22),B$(15),T$(6):IF NG THEN 20250
20200 FOR Z=1 TO N%:GOSUB 25000:PRINT"{down:2}PLAYER#"Z":":INPUT"INPUT YOUR NAME PLEASE->";N$(Z-1)
20210 INPUT"LEVEL OF DIFFICULTY (1-4) 4=HARD       {left:7}";DF
20215 IF DF<1 OR DF>4 THEN PRINT"{up}";:GOTO 20210
20220 W%(Z)=DF:NEXT:GOSUB 25000:PRINT:PRINT
20230 INPUT"MAP LEVEL DIFFICULTY (1 TO 20)         {left:9}";DF
20240 DF=DF*5:IF DF<5 OR DF>100 THEN PRINT"{up}";:GOTO 20230
20250 PRINT"{down:4}      HIT ANY KEY TO CONTINUE"
20260 GET X$:IF X$=""THEN 20260
20265 PRINT"{clear}{down:5}       {rvrs on}STAND BY: FILLING ARRAYS.{rvrs off}"
20270 FOR X=0 TO 4:READ A$(X):NEXT:FOR I=0 TO 15:READ B$(I):NEXT
20275 CS$="{black}{clear}{blue}{down:21}"
20280 K%(2,0)=8:FOR I=0 TO 15
20290 X=RND(1)*4:Y=RND(1)*4:IF K%(X,Y)>0 THEN 20290
20300 K%(X,Y)=I+8*(I=8):NEXT
20310 FOR X=0 TO 16:READ O$(X):READ O(X):NEXT
20320 FOR S=1 TO 4:FOR X=0 TO 10:READ Z:FOR Y=0 TO N1:IF W%(Y+1)=S THEN I%(Y,X)=Z
20330 NEXT:NEXT:NEXT
20340 FOR X=0 TO 22:READ I$(X):NEXT
20350 IF NG=0 THEN 21000
20400 PRINT:PRINT"GET WHICH GAME (A-G)?";
20405 GET X$:IF X$=""THEN 20405
20410 PRINT X$;:IF ASC(X$)>71 OR ASC(X$)<65 THEN PRINT"{left} {left}";:GOTO 20405
20415 PRINT:PRINT
20420 OPEN 1,8,15,"I":CLOSE 1
20425 OPEN 2,8,2,"0:"+X$+",S,R":INPUT# 2,N1:FOR P=0 TO N1:INPUT# 2,N$(P):FOR X=0 TO 22
20430 INPUT# 2,I%(P,X):NEXT:INPUT# 2,P%(P,0):INPUT# 2,P%(P,1):NEXT:FOR S=0 TO 3:FOR X=0 TO 3
20435 INPUT# 2,K%(S,X):NEXT:INPUT# 2,S%(S,0):INPUT# 2,S%(S,1):NEXT:INPUT# 2,T:CLOSE 2
20440 OPEN 2,8,2,"0:MAP."+X$+",S,R":FOR X=50176 TO 51999:INPUT# 2,Z:POKE X,Z:NEXT:CLOSE 2
21000 PL=167:XA=168:YA=169:XB=170:YB=171:CO=180:LO=249:HI=250:DU=251:SN=252
21010 CL=52224:GR=CL+3:TE=GR+3:P1=TE+3:P2=P1+3:S1=P2+3:S2=S1+3:SH=S2+3:SE=SH+3
21020 SA=SE+3:AL=SA+3:IN=AL+3:TN=IN+3:TU=TN+3
21025 IF NG THEN 21200
21035 REM--DO MOUNTAINS
21040 L=DF:POKE CO,9:FOR I=1 TO 3:FOR S=0 TO 3:GOSUB 10:IF I=1 THEN S%(S,0)=X:S%(S,1)=Y
21050 NEXT:NEXT
21060 REM--DO LAKES
21070 POKE CO,14:FOR I=1 TO 3:FOR S=0 TO 3:GOSUB 10:NEXT:NEXT
21080 REM--DO TOWNS
21090 L=1:POKE CO,7:FOR I=1 TO 3:FOR S=0 TO 3:GOSUB 10:NEXT:FOR S=1 TO 2:GOSUB 10:NEXT:NEXT
21100 REM--DO SANCTUARIES
21110 POKE CO,4:FOR I=1 TO 3:FOR S=0 TO 3:GOSUB 10:NEXT:FOR S=1 TO 2:GOSUB 10:NEXT:NEXT
21120 REM--DO WK CASTLE
21130 X=INT(RND(1)*20+10):Y=INT(RND(1)*20+10):POKE CO,6:POKE XA,X:POKE YA,Y:SYS P2
21140 POKE YA,Y+1:SYS P2:POKE XA,X+1:SYS P2:POKE YA,Y:SYS P2
21142 REM--PUT MAGIC ITEMS BACK ON MTS.
21144 POKE CO,9:FOR X=0 TO 3:POKE XA,S%(X,0):POKE YA,S%(X,1):SYS P2:NEXT
21150 REM--DO PLAYERS
21160 FOR P=0 TO N1
21170 X=INT(RND(1)*20+10):Y=INT(RND(1)*20+10):POKE XA,X:POKE YA,Y:SYS S2:C=PEEK(CO)
21180 IF C<>5 THEN 21170
21190 P%(P,0)=X:P%(P,1)=Y:GOSUB 900:NEXT
21200 T$(0)="{orange}{rvrs on}    {down}{left:4}    {rvrs off}{up}"
21210 T$(1)="{lt. green}{$c0}{$c1}{$c2}{$c3}{down}{left:4}{$c4}{$c5}{$c6}{$c7}{up}"
21220 T$(2)="{lt. red}{$c8}{$c9}{$ca}{$cb}{down}{left:4}{$cc}{$cd}{$ce}{$cf}{up}"
21230 T$(3)="{lt. blue}{$d0}{$d1}{$d2}{$d3}{down}{left:4}{$d4}{$d5}{$d6}{$d7}{up}"
21240 T$(4)="{gray3}{$d8}{$d9}{$da}{$db}{down}{left:4}{$dc}{$dd}{$ff}{$df}{up}"
21250 T$(5)="{lt. green}{$a0}{$a1}{$a2}{$a3}{down}{left:4}{$a4}{$a5}{$a6}{$a7}{up}"
21260 T$(6)="{brown}{shft pound}{ctrl pound}{$aa}{$ab}{down}{left:4}{$ac}{$ad}{$ae}{$af}{up}"
22000 GOTO 300
23000 DATACHEST,LEFT ARM,RIGHT ARM,LEFT LEG, RIGHT LEG
23010 DATATHE BARRACKS,A DINING HALL,THE TORTURE CHAMBER,A SMALL HALLWAY
23020 DATATHE MASTER BEDROOM,A LARGE LIBRARY,THE CELLAR,A KITCHEN,THE COURT ROOM
23030 DATAA BEDROOM,AN AMORY,WK,THE GUARD ROOM,THE TROPHY ROOM,A LARGE HALLWAY,T
23040 DATAWARRIOR,1,BANDIT,.7,WEREBEAR,5,GOBLIN,.3,ORC,.5,TROLL,10,OGRE,16
23050 DATAEARTH GIANT,20,WIZARD,25,HACKER,50,DRAGON,100,WITCH KING,150,HYDRA,40
23060 DATAGORGON,30,HOBGOBLIN,.6,ELVE,2,DWARVE,3
23070 DATA30,1,2,3,50,50,1,3,1,0,1
23080 DATA30,1,1,2,50,40,0,2,0,0,1
23090 DATA25,1,0,1,40,30,0,1,0,0,1
23100 DATA20,1,0,0,30,20,0,0,0,0,0
23110 DATARATION,TELEPORT SPELL,CLERIC,SCOUT,GOLD PIECE,WARRIOR
23120 DATASWORD (DRAGON SLAYER),RAIDER,SPELL OF SEEING,RAFT,MULE,WIZARD
23130 DATAHORN OF OPENING,BOOTS OF STEALTH,ARMOUR OF DEFENSE,SWORD OF STRENGTH
23140 DATA,HAMMER OF THOR,TALISMAN OF SPEED,SPELL OF SEEKING,HORSES,ELVE,DWARVE
24999 END
25000 PRINT"{clear}::::::::::::::::::::::::::::::::::::::::"
25010 PRINT":      FORTRESS OF THE WITCH KING      :"
25020 PRINT"::::::::::::::::::::::::::::::::::::::::":PRINT
25030 PRINT"COPYRIGHT (C) 1984 BY THE AVALON HILL"
25040 PRINT"GAME CO.  ALL RIGHTS RESERVED."
25050 PRINT"COMPUTER PROGRAM & AUDIO VISUAL DISPLAY"
25060 PRINT"COPYRIGHTED.  DESIGNED BY M. E. MEHLICH"
25065 PRINT"COMMODORE 64 VERSION BY DYADIC SOFTWARE"
25070 PRINT:PRINT" AN ADVENTURE FOR ONE TO FOUR PLAYERS.":RETURN
34816 {
